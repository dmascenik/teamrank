plugins {
  id "com.moowork.node" version "0.10"
}

configurations {
  mockRestService
}

dependencies {
  mockRestService project(path: ":web", configuration: "mockRestService")
}

def distFileName = 'app.zip'

task clean(type: Delete) {
  description 'Deletes the build directory'
  delete 'build'
}

task compile(type: NpmTask) {
    args = ['run', 'compile']

}
compile.dependsOn npmInstall

task test(type: NpmTask) {
    args = ['run', 'test']

}
test.dependsOn compile

task coverage(type: NpmTask) {
    args = ['run', 'coverage']

}
coverage.dependsOn compile

task build(type: NpmTask) {
	args = ['run', 'package']
}
build.dependsOn coverage

task run(dependsOn: 'unpackMockRestService', type: NpmTask)  {
  args = ['run', 'start']
}

task unpackMockRestService(type: Copy) {
  dependsOn configurations.mockRestService
  from { 
    configurations.mockRestService.collect { zipTree(it) }
  }
  into 'build/mockRestService'
}

/**
 * Final artifacts (JS, HTML, CSS, assets, etc) are zipped into a single artifact
 */
task zipDist(dependsOn: 'build', type: Zip) {
  from 'build/dist'
  from 'src/static'
  archiveName = distFileName
  destinationDir = file('build')
}

configurations {
  dist
}

artifacts {
  dist zipDist
}
